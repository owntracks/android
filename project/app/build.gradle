apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

def versionMajor = 2
def versionMinor = 0
def versionPatch = 0
def versionBuild = 22
ext.pahoVersion = "1.2.0"
ext.daggerVersion = '2.10'
ext.playLibVersion = '11.8.0'
ext.okHttpVersion = '3.8.0'
ext.supportLibVersion = '27.1.0'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "org.owntracks.android"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [eventBusIndex: 'org.owntracks.android.EventBusIndex']
            }
        }
        resConfigs "en", "de"
    }

    buildTypes {
        release {
            useProguard true
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "GOOGLE_MAPS_API_KEY", "${google_maps_api_key}"
        }

        debug {
            useProguard true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.debug'
            resValue "string", "GOOGLE_MAPS_API_KEY", "${google_maps_api_key}"
        }
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java', 'build/generated/source/greendao/org/owntracks/android/db']
        }
    }

    signingConfigs {
        debug {
            keyAlias 'debug.owntracks.org'
            keyPassword '__DEBUG__'
            storeFile file('./debug-keystore.jks')
            storePassword '__DEBUG__'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }

    lintOptions {
        baseline file("../../lint/lint-baseline.xml")
        checkAllWarnings true
        warningsAsErrors false
        abortOnError false
        disable 'TypographyFractions', 'TypographyQuotes', 'Typos'

    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed"
                exceptionFormat "full"
            }
        }
    }
}

dependencies {
    // Support libraries
    compile "com.android.support:support-v4:${supportLibVersion}"
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:preference-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile 'com.android.support.constraint:constraint-layout:1.1.0'

    // Play Services libraries
    compile 'com.firebase:firebase-jobdispatcher:0.8.5'
    compile "com.google.android.gms:play-services-maps:${playLibVersion}"
    compile "com.google.android.gms:play-services-location:${playLibVersion}"
    compile "com.google.android.gms:play-services-places:${playLibVersion}"

    // Utility libraries
    compile "com.google.dagger:dagger:${daggerVersion}"

    compile 'org.greenrobot:eventbus:3.1.1'
    compile 'org.greenrobot:greendao:3.2.2'
    compile 'org.altbeacon:android-beacon-library:2.13.1@aar'

    compile "org.eclipse.paho:org.eclipse.paho.client.mqttv3:${pahoVersion}"
    compile "com.squareup.okhttp3:okhttp:${okHttpVersion}"
    compile "com.squareup.okhttp3:logging-interceptor:${okHttpVersion}"
    compile "com.fasterxml.jackson.core:jackson-core:2.9.3"
    compile "com.fasterxml.jackson.core:jackson-databind:2.9.3"
    compile "com.jakewharton.timber:timber:4.6.1"
    compile "com.github.joshjdevl.libsodiumjni:libsodium-jni-aar:1.0.8"

    // Widget libraries
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    compile 'com.afollestad.material-dialogs:core:0.9.6.0'
    compile 'com.afollestad.material-dialogs:commons:0.9.6.0'
    compile('com.mikepenz:materialdrawer:6.0.2@aar') { transitive = true }
    implementation "com.android.support:appcompat-v7:${supportLibVersion}"
    implementation "com.android.support:recyclerview-v7:${supportLibVersion}"
    implementation "com.android.support:support-annotations:${supportLibVersion}"
    implementation "com.android.support:design:${supportLibVersion}"
    implementation "com.mikepenz:materialize:1.1.2@aar"

    // Preprocessors
    annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.1.1'
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.8.9'

    testCompile 'org.powermock:powermock-module-junit4:1.7.1'
    testCompile 'org.powermock:powermock-api-mockito2:1.7.1'
}


greendao {
    schemaVersion 15
}

tasks.withType(Test) {
    systemProperty "java.library.path", "jniLibs/x86_64"
}